\ - Object stack
: bpage here $ff and 256 swap - allot here $100 allot constant ;
bpage os
variable osp  os osp !
: >o osp @ ! 4 osp c+! ;
: o> -4 osp c+! osp @ @ ;

\ - Structs
: field create over , + does> @ + ;

\ - Addressing
0 value me
: as to me ;
: { me >o to me ;
: } o> to me ;
: -> ' >body @ ?literal s" +" evaluate ; immediate 
: require me { require } ;
: include me { include } ;
: included me { included } ;

\ - Vars
0 value /obj
0 value /basis
: ofield /obj swap field to /obj does> @ me + ;
: var cell ofield ;
: motif /basis to /obj ;

\ - Double-linked tree
var next var prev var first var last var parent
include lib/dltree.vfx
: remove dlremove ;
: push dlpush ;
: unshift dlunshift ;
: insert dlinsert ;
: each dleach ;
: (each>) r> swap dleach ;
: each> postpone (each>) discard-sinline ; immediate
: clear dlclear ;

\ - Basis
var en var hide var halt
var x var y var w var h var vx var vy var sx var sy
var beh var drw var phy
/obj to /basis
defer children

\ - Instancing
: init en on 1. dup sx v! ;
: *child here 256 allot&erase dup rot push dup { init }  ;
: obj create here as 256 allot&erase init ;

\ - Basic actions
: reckon
    first @ {  begin  me while
        next @ >r
            recurse
            en @ 0= if me remove  then
        r> as
    repeat }
;
: ?execute ?dup 0; catch .throw ;
: +t +m x v@ 2i 2p>f sx v@ 2p>f 0e transform ;
: -t -m ;
: draw en @ 0; hide @ ?; x v@ 2p>s at drw @ ?execute children ; 
: act halt @ ?;  en @ 0;  beh @ ?execute  me each>  recurse ;
: physics halt @ ?;  en @ 0;  phy @ ?execute  me each>  recurse ;
: (draw>) r> drw ! ; : draw> postpone (draw>) discard-sinline ; immediate
: (act>) r> beh ! ; : act> postpone (act>) discard-sinline ; immediate
:noname [ is children ] first @ 0; +t ['] draw me each -t ;

\ - Indexed child access
: #children 0 first @ ?dup 0; begin 1 u+ -> next @ ?dup 0= until ;
: nth-child first @ dup 0= if nip exit then swap 0 ?do -> next @ loop ;
