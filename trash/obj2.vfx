create os  $100 allot
variable >os 
: >o  >os @ $ff and os + !  4 >os +! ;
: o>  -4 >os +!  >os @ $ff and os + @ ;

\ ------------------------------------------------------------------------------

: field  create over , +  does> @ + ;

\ ------------------------------------------------------------------------------

0 value me
0 value /kind
: var  dup @ cell field over !  does> @ me + ;
: as  to me ;
: [[  me >o  as ;  DoNotSin
: ]]  o> as ;  DoNotSin
: kind  me ;
: ?execute  ?dup 0= abort" Unimplemented method!" execute ;
: ?have  >in @  have abort" Method already defined!" >in ! ;
: do-method  @ kind @ + @ ?execute ;
: method  ?have /kind cell field to /kind $C0FFEE , does> do-method ;
: metafield  /kind swap field to /kind does> @ + ;
: ?method  >in @  ' >body cell+ @ $C0FFEE <> abort" Not a method."  >in ! ;
: m:  ( kind -- <method> <code> ; )  ?method  ' >body @ + :noname swap ! ;
: sizeof  @ ;
: ?literal  state @ if postpone literal then ;
: ->  ' >body @ ?literal s" +" evaluate ; immediate

\ ------------------------------------------------------------------------------

cell metafield kind.size
cell metafield kind.super
32 metafield kind.source

create %obj  4 , 1016 allot&erase

\ ------------------------------------------------------------------------------

: require  me [[ require ]] ;
: include  me [[ include ]] ;
: included  me [[ included ]] ;

: kind:  ( -- <name> kind )
    ( super ) dup
    >in @  have if  >in !  ' >body  else  >in !  create  here  then
        dup >r  1024 move 1024 allot  r@ cell+ !
        CurrSourceName r@ kind.source place
        r> ;
: ;kind  ( kind -- ) drop ;
: update  kind.source count included ;

\ ------------------------------------------------------------------------------

method (remove) ( -- )
method (push) ( list -- )
method (unshift) ( list -- )
method (insert) ( dest -- )
method (each) ( xt -- ) ( me=obj -- )
\ method (each-while) ( xt -- ) ( me=obj -- ? )
method (clear) ( -- )

: remove  ( obj -- )  [[ (remove) ]] ;
: push  ( v list -- )  [[ (push) ]] ;
: unshift  ( v list -- )  [[ (unshift) ]] ;
: insert  ( v dest -- )  [[ (insert) ]] ;
: each>  ( obj -- <code> )  [[ r> (each) ]] ; DoNotSin
\ : each-while>  ( obj -- <code> )  [[ r> (each-while) ]] ; DoNotSin
: clear  ( obj -- )  [[ (clear) ]] ;
: each  ( xt obj -- ) [[ (each) ]] ;

\ ------------------------------------------------------------------------------

%obj kind: %dlnode
    var next var prev var first var last var parent
;kind

\ ------------------------------------------------------------------------------

: dlremove  ( node -- )
    dup -> parent @ dup 0= if  2drop  exit  then
    locals| p n |
    0 n -> parent !
    p -> first @ p -> last @ = if
        n p -> first @ = if
            0 dup p -> first ! p -> last !
        exit then
        -1 abort" dlremove: Corrupt tree!"
    else
        n p -> first @ = if
            0 n -> next @ -> prev !
            n -> next @ p -> first !
            0 dup n -> prev ! n -> next !
        exit then
        n p -> last @ = if
            0 n -> prev @ -> next !
            n -> prev @ p -> last !
            0 dup n -> prev ! n -> next !
        exit then
        n -> next @ n -> prev @ -> next !
        n -> prev @ n -> next @ -> prev !
        0 dup n -> prev ! n -> next !
    then ;

: (dlinit)  ( node parent -- )
    locals| p n |
    n dup p -> first ! p -> last !
    p n -> parent ! ;

: dlpush  ( node parent -- )
    over dlremove
    dup -> first @ 0= if  (dlinit)  exit  then
    locals| p n |
    n p -> last @ -> next !
    p -> last @ n -> prev !
    n p -> last !
    p n -> parent ! ;

: dlinsert  ( node dest -- )
    dup -> next @ 0= if  -> parent @ dlpush  exit  then
    over dlremove
    dup -> next @ locals| e d n |
    n d -> next !
    d n -> prev !
    e n -> next !
    n e -> prev !
    d -> parent @ n -> parent ! ;

: dlunshift  ( node parent -- )
    over dlremove
    dup -> first @ 0= if  (dlinit)  exit  then
    locals| p n |
    p -> first @ n -> next !
    n p -> first @ -> prev ! 
    n p -> first !
    p n -> parent ! ;

: dleach  ( xt node -- )
    -> first @ [[  begin  me while
        dup >r execute r>  next @ as
    repeat
    drop  ]] ;

: (dlorphan)  0 dup next ! dup prev ! parent ! ;

: dlclear  ( node -- )
    [[ first @ [[  begin  me while
        next @  (dlorphan)  as
    repeat
    ]] 0 dup first ! last ! ]] ;

\ ------------------------------------------------------------------------------

: >first  -> first @ ;
: >next  -> next @ ;

\ ------------------------------------------------------------------------------

%dlnode m: (remove)  me dlremove ;
%dlnode m: (push)  me dlpush ;
%dlnode m: (unshift)  me dlunshift ;
%dlnode m: (insert)  me dlinsert ;
%dlnode m: (each)  me dleach ;
%dlnode m: (clear)  me dlclear ;

\ ------------------------------------------------------------------------------

method draw ( -- )
method step ( -- )
method physics ( -- )
method respond ( -- )  \ event handler

\ ------------------------------------------------------------------------------

%dlnode kind: %actor  \ basis for all visual objects (except maybe particles)
    var en var x var y var z var w var h var d var vx var vy var vz 
    var stp var drw var phy var rsp
;kind

: draw>  r> drw ! ;  DoNotSin
: step>  r> stp ! ;  DoNotSin
: physics>  r> phy ! ;  DoNotSin
: respond>  r> rsp ! ;  DoNotSin

\ ------------------------------------------------------------------------------

defer alloc

: heap  ['] allocate is alloc ;
: allotment  here swap allot&erase 0 ;
: static  ['] allotment is alloc ;
static

\ ------------------------------------------------------------------------------

: kill  -> en off ;
: sweep   
    -> first @ [[  begin  me while
        next @ >r
            en @ 0= if  me recurse  me free drop  me remove  then
        r> as
    repeat ]] ;

\ ------------------------------------------------------------------------------

0 value joan
: ?child  joan if  me joan push  then ;
: in  to joan ;

\ ------------------------------------------------------------------------------

method init ( - )
%obj m: init ;
%dlnode m: init ;
%actor m: init  at@f x fv!  en on  ?child ;

: supers  0 locals| n |  kind @ begin  kind.super @ ?dup while
    dup -> init @ swap  1 +to n  repeat  n 0 ?do  execute  loop ;
: (init)  kind !  supers  init ;
: oneof  dup sizeof  alloc throw [[ (init) me ]] ;
: static,  here over sizeof allot&erase  as  (init) ;

\ ------------------------------------------------------------------------------

: ?draw  en @ if  draw  then ;
: ?draw-children
    first @ 0= if exit then
    x fv@ m al_translate_transform  m al_use_transform 
    ['] ?draw (each)
    x fs@ fnegate y fs@ fnegate m al_translate_transform  m al_use_transform ;

\ ------------------------------------------------------------------------------

: ?execute  ?dup if execute then ;

%actor m: draw  x v@ at  drw @ ?execute  ?draw-children ;
%actor m: step  stp @ ?execute  me each>  en @ if  step  then ;
%actor m: physics  phy @ ?execute  me each>  en @ if  physics  then ;
%actor m: respond  rsp @ ?execute  me each>  en @ if  respond  then ;

\ ------------------------------------------------------------------------------

method (.me)

: .name  >name count 16 min type space ;
: .actor  me .name  x sf@ f. y sf@ f. ;
: .me  kind @ .name  me . (.me) ;

%obj m: (.me) ;
%dlnode m: (.me)  ;
%actor m: (.me) .actor ;

: peek  [[ .me ]] ;
: .children  ( obj - )  each>  cr me . me .name ;

\ ------------------------------------------------------------------------------

\\

: dlnode  create %dlnode static drop ;

dlnode p1
dlnode n1
dlnode n2
dlnode n3

\ : hi  -> first @ begin ?dup while dup . -> next @ repeat ;
: hi  each> me . ;

n1 p1 dlpush
n2 p1 dlpush
n3 p1 dlpush

\ n2 dlremove