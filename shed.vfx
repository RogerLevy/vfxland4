require alpha.vfx
require obj.vfx
require core.vfx
require lib/gradient.vfx
require ui.vfx
ui root push

0 value dragee
0 value this
0 value hoveree
variable snapping
0 value always-drag?

\ - game config
: matte x v@ 2p>s gamew zoom sf* gameh zoom sf* al_set_clipping_rectangle ;
: center
    winw 2 /  gamew zoom sf* 2 / -
    winh 64 - 2 /  gameh zoom sf* 2 / -  2s>p game -> x v!
    zoom f>p dup game -> sx v!
;
game as :> act> center zoom f>p dup sx v! draw> matte ;

\ - Scripting
motif var con var ser /obj to /basis
: (physics>) r> phy ! ;
: physics> postpone (physics>) discard-sinline ; immediate
: ?literal state @ if postpone literal then ;
: 1object body> con ! pen v@ 2s>p x v! physics> vx v@ 2delta* x v+! ;
: object:
    create here 0 , :noname swap !
    does> me *child { dup 1object @ execute me } ;
: kill -> en off ;
object: *generic ;
: ??? me ;

\ - Stage
game { *generic named stage }

\ - Quickly reload a set file
create reload$ 64 allot
: reload cr ." ==================== RELOAD ====================" reload$ count included ;
s" shed.vfx" reload$ place
: fget 0 parse reload$ place s" .vfx" reload$ append reload$ count included ;

\ - Dragging
: scale* parent @ ?dup if { sx v@ 2p* recurse } then ; 
: +drag walt 2s>p 1. 1. scale* 2p/ x v+! ;
: bounding w v@ scale* ;
: dragging alt? if stage else dragee then { +drag } ;
: tsnap dup abs 8. + dup 16. mod - swap 0< if negate then ;
: ?snap snapping @ if tsnap swap tsnap swap else 2i then ;
: plop alt? if stage else dragee then ?dup 0; { x v@ ?snap x v! } 0 to dragee ;
: gbox x v@ w v@ area v*m 2swap v*m 2swap ;
: (hovering) me each> hide @ ?; w v@ or 0<> first @ or 0; mouse 2s>p gbox inside? if me to hoveree then +t recurse -t ;
: hovering 0 to hoveree (hovering) ;
: pick to this ;
: drag to dragee ;

\ - Serialize
variable indent
: +i  4 indent +! ;
: -i  -4 indent +! ;
: cri cr indent @ 0 ?do space loop ;
: .xy x v@ 2p>s swap . . ;
: ?.wh w v@ or if w vp? ." w v! " then ;
: ?.s sx v@ or 1. <> if sx vp? ." sx v! " then ;
: .con con @ >name count type space ;
: .{ ." { " ;
: .} ." } " } ;
: .}r first @ if cri then .} ;
: ?.named nam @ if ." named " me .name then ;
: (o.) .xy ." at " .con ?.named .{ ?.wh ?.s ;
: o. { (o.) .} ;
: o.r cri { (o.) ser @ ?execute space ['] .}r >r me each> +i me recurse -i ;
: figpath z[ ." fig/" type ." .vfx" ]z zcount ;
: save figpath f[ game o.r ]f ;
: save-to 0 parse save ;
: load 0 pick game clear game { 0 parse figpath included } ;
: ser> r> ser ! ;

\ - Info
: stext @color >r 1 1 +at dup black text -1 -1 +at r> color text ;
: a+t
    +m identity
    parent @ begin ?dup while
        dup { x v@ 2p>f  sx v@ 2p>f } 0e
            m2 al_build_transform  m m2 al_compose_transform
        -> parent @
    repeat
    m al_use_transform
;
: tiny a+t x v@ v*m 2p>s at -t identity ;
: info +m tiny 0 -33 +at sherry32.fnt font ! z[ me o. ]z stext -m ;
: outline a+t x v@ 2p>s at w v@ 2p>s rect -t ;

\ - App support
obj apps  me root push
ui root push \ bring ui to front


\ - UI extension
: ?drag dragee alt? 1 mheld? and or if nip dragging else { hovering } then ;
: outlines
    hoveree ?dup if { yellow outline } then
    this ?dup if { red outline white ui? if info then } then
;
uiext as *generic named shed {
:noname
    act>
        always-drag? not if ui? 0; then root ?drag
    draw>
        always-drag? not if ui? 0; then outlines
; execute

: ?delete this ?dup 0; kill 0 pick 0 drag 0 to hoveree root { reckon } ;
:noname
    respond>
    ALLEGRO_EVENT_DISPLAY_SWITCH_IN happened? if
    then
    ALLEGRO_EVENT_KEY_DOWN happened? if
        the-key <f5> = if s" reload" evaluate then
        the-key <del> = if ?delete then
    then
    ALLEGRO_EVENT_MOUSE_BUTTON_DOWN happened? if
        hoveree ?dup if dup pick drag then
    then
    ALLEGRO_EVENT_MOUSE_BUTTON_UP happened?
    ALLEGRO_EVENT_DISPLAY_SWITCH_OUT happened? or if
        plop
    then
; execute
}