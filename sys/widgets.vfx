_attributes bit _click bit _nr to _attributes
motif var data +basis 

z" sys/data/MxPlus_IBM_BIOS.ttf" #8 3 font: ascii8x8.fnt
z" sys/data/MxPlus_IBM_VGA_8x16.ttf" #16 3 font: ibm8x16.fnt

: !font ascii8x8.fnt font ! ;
: boxshadow 5 5 +at   sdims $00000040 color rectf   -5 -5 +at ;

: ?click hoveree me = 0; _click aon ;
: exec-data zdata@ ?dup 0; zcount evaluate ;
: +.vfx pad place s" .VFX" pad append pad count ;
: ?parent-wordlist
    me { begin parent @ dup while
        in nam @ ?dup if count +.vfx search-context if >body @ } exit then then
    repeat } 
;
: button:exec
    ?parent-wordlist dup >r if r@ +order then
    _click aoff ['] exec-data catch dup .throw if 2drop then
    r> if previous then
;
: button-respond respond>  
    ALLEGRO_EVENT_MOUSE_BUTTON_DOWN happened? the-mbutton 1 = and if over? 0; ?click then
    ALLEGRO_EVENT_MOUSE_BUTTON_UP happened? the-mbutton 1 = and if _click a? 0; button:exec then
;
: (down) 2 2 +at sdims 2dup grey <d rectf grey rect ;
: (up) boxshadow sdims 2dup grey rectf white rect ;
: button-draw draw> _click a? if (down) else (up) then 16 10 +at black zdata@ !font text ;

: ?expand x v@ w v@ 2+ 8. 8. 2+ parent @ { w v@ 2max w v! } ;
: ?nr _nr a? 0; next @ 0; 8. y @ h @ + 8. + next @ -> x v! ;
: ?layout _bolted a? 0; next @ if x @ w @ + 8. + y @ next @ -> x v! then ?nr ?expand ;

: .widget
    data @ ?dup if ." ` " icount type ." ` " then
    bgc @ ?dup if h. ." bgc ! " then
;
: ser-widget ser> .widget ;
: 1w h @ 32. >= ?; data @ ?dup 0; count 8. * 32. + w ! drop ;
: ` [char] ` parse data! 1w ;

: label-draw draw> white zdata@ !font stext ?layout ;

: open-toolbox -> first @ on ;
: toolbox-respond respond>
    over? 0;
    click? if
        the-mbutton 1 = if
            first @ ?dup 0; toggle 
        then
    else
        ALLEGRO_EVENT_MOUSE_BUTTON_DOWN happened? if
            the-mbutton 1 = if
                me pickdrag   me downstage
            then
        then
    then
;
: toolbox-draw draw> -1 -1 at white sdims 1 1 2+ rectf 2 2 +at black sdims 2 2 2- rect sdims drop 2 - 7 black rectf 15 14 +at zdata@ !font text ;

: panel-draw draw> bgc @ color  sdims    2dup rectf   white rect ;

: textbox-draw draw>
    data @ 0;
    sdims bgc @ color rectf
    4 4 +at
    ibm8x16.fnt font !
    zdata@ !font  w @ p>s 8 -  black mltext
;

: group-draw draw> editmode @ 0; blue 8 circf yellow 8 circ ;

c: *label 16. h ! [bolted] label-draw ser-widget act> ?layout ;
c: *textbox 160. 160. w v! $fff8a0e0 bgc ! textbox-draw ser-widget $[ ." The rain " cr ." in Spain falls mainly on the plain." ]$ data! ;
c: *button 28. h ! [bolted] button-draw ser-widget button-respond act> ?layout ;
c: *toolbox 128. 28. w v! toolbox-draw ser-widget toolbox-respond ;
c: *panel _bolted aon $00000040 bgc ! panel-draw act> 0 0 w v! ;
c: *spanel _bolted aon $00000040 bgc ! panel-draw ;
c: *group 16. 16. w v! group-draw ;

\ : [toolbox] *toolbox 0 32 at ;
\ : [panel] *panel 8 8 at ;

: [nr] _nr aon ;
